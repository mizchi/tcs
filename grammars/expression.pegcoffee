//////////////
// Expressions
//////////////

// See https://developer.mozilla.org/ja/docs/Web/JavaScript/Reference/Operators/Operator_Precedence

expr = assignmentable

assignable = memberExpr
assignmentable
  = _assignment / binaryExpr
  _assignment = left:assignable _ op:_assignmentOp _ right:expr
  {
    type: 'AssignmentExpression'
    left: left
    operator: op
    right: right
  }
  _assignmentOp = $("=" / "+=" / "-=" / '*=' / '/=')

binaryExpr = equality

callArgumentList = first:expr rest:(_ ',' _ e:expr {e})*
  {
    [first].concat(rest)
  }

newExpr = "new" __ e:memberExpr _ "(" _ args:callArgumentList? _ ")"
  {
    type: 'NewExpression'
    callee: e
    arguments: args ? []
  }

callExpr = callee:memberExpr "(" _ args:callArgumentList? _ ")"
  {
    type: "CallExpression"
    callee: callee
    arguments: args ? []
  }

memberExpr = first:primary accesses:_memberAccess*
  {
    accesses.reduce (node, access) ->
      type: 'MemberExpression'
      computed: false
      object: node
      property: access
    , first
  }
  _memberAccess
    = '.' id:identifier {
      id
    }
  // dynamic member access

equality =
  left:logically _ op:_equalityOperator _ right:equality {
    type: 'BinaryExpression'
    left: left
    operator: op
    right: right
  }
  / logically
  _equalityOperator = $("===" / "==" / "!==" / "!=")

logically =
  left:additive _ op:_logicallyOperator _ right:logically {
    type: 'BinaryExpression'
    left: left
    operator: op
    right: right
  }
  / additive
  _logicallyOperator = $(">" / ">="/ "<" / "<=")

additive =
  left:multiplicative _ op:_additiveOperator _ right:additive {
    type: 'BinaryExpression'
    left: left
    operator: op
    right: right
  }
  / multiplicative
  _additiveOperator = $("+" !"=" / "-" !"=" )

multiplicative =
  left:prefixable _ op:_multicativeOperator _ right:multiplicative {
    type: 'BinaryExpression'
    left: left
    operator: op
    right: right
  }
  / prefixable
  _multicativeOperator = $("*" !"=" / "/" !"=" / "%" !"=")

leftHandSideExpr = callExpr / memberExpr / newExpr

prefixable
  = postfixable
  / ops:(op:_prefixOp _ {op})+ e:postfixable {e}
  _prefixOp = "++" / "--" / "+" / "-" / "!" / "~" / "typeof" / "delete"

postfixable = e:leftHandSideExpr ops:_postfixOp* {e}
  _postfixOp = "++" / "--"

primary
  = arrowFuncExpr
  / thisExpr
  / identifier
  / literal
  / arrayExpr
  / objectExpr
  / "(" _ e:expr _ ")" {e}

// atoms

thisExpr = '@' {type: 'ThisExpression'}

identifier = !reserved name:symbol{
  type: 'Identifier', name:name
}

identifierWithTypeAnnotation = !reserved name:symbol ann:typeAnnotation? {
  type: 'Identifier', name:name, typeAnnotation: ann
}

objectKey = identifier / literal
property
  = key:objectKey _ ':' _ expr:expr
    {
      type: 'Property'
      key: key
      value: expr
      kind: 'init'
      method: false
      shorthand: false
      computed: false
    }

objectExpr =
  "{" _ EOL? _ properties:(head:property rest:(_ (',' / EOL) _ EOL? _ p:property {p})* {[head].concat(rest)})? _ EOL? _ "}"
  {
    type: 'ObjectExpression'
    properties: properties ? []
  }


arrowFuncParamList = h:arrowFuncParam rest:(_ "," _ p:arrowFuncParam {p})* {[h].concat(rest)}
arrowFuncParam = identifier

arrowFuncExpr
  = "(" _ params:arrowFuncParamList? _ ")" _ "=>" _ expr:expr
    {
      type: "ArrowFunctionExpression"
      id: null
      params: params ? []
      defaults: []
      body: expr
      rest: null
      generator: false
      expression: true
    }
  / "(" _ params:arrowFuncParamList? _ ")" _ "=>" _ EOL body:block
    {
      type: "ArrowFunctionExpression"
      id: null
      params: params ? []
      defaults: []
      body:
        type: 'BlockStatement'
        body: wrapBody(body)
      rest: null
      generator: false
      expression: false
    }

arrayExpr = '[' _ exprs:(first:expr rest:(_ "," _ e:expr {e})* {[first].concat(rest)})? _ ']'
  {
    type: 'ArrayExpression'
    elements: exprs ? []
  }
