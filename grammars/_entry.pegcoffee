// Indent Style Syntax
{
  global.ctx = {}
  ctx.indentStack = []
  ctx.indent = ""

  global.buildTree = (first, rest, builder) ->
    result = first
    for i in rest
      result = builder result, i
    result

  global.buildBinaryExpression = (first, rest) ->
    buildTree first, rest, (result, element) ->
      {
        type:     "BinaryExpression",
        operator: element[1]
        left:     result
        right:    element[3]
      }

  r = (node) ->
    node.raw = text().replace /[\uEFEF\uEFFE\uEFFF]/g, ''
    node
  # store position information
  p = (node) ->
    node.line = line()
    node.column = column()
    node.offset = offset()
    node

  # composition of r and p
  global.__rp = (node) -> r p node

}

start = self:program {
  global.ctx = {}
  ctx.indentStack = []
  ctx.indent = ""
  self
}

program = lines:topLevelLine* __blank { __rp type: 'program', body: lines}

topLevelLine
  = SAMEDENT node:(!EOL s:topLevelStatement {s}) comment? EOL* {node}

line
  = SAMEDENT node:(!EOL s:statement {s}) comment? EOL* {node}

block = children: ( INDENT c:line* DEDENT { c }) {children}

EOL = "\r\n" / "\n" / "\r"

comment = '//' value:toTheEndOfLine EOL?
  {
    __rp({type: 'comment', value})
  }

toTheEndOfLine = !comment text:$((!EOL .)+) {text}

SAMEDENT
  = i:[ \t]* &{
    i.join('') is ctx.indent
  }

INDENT
  = &(
      i:[ \t]+ &{
        i.length > ctx.indent.length
      }
      {
        ctx.indentStack.push(ctx.indent)
        ctx.indent = i.join("")
      }
    )

DEDENT
  = {
    ctx.indent = ctx.indentStack.pop()
  }

// Utils
space = ' '
_ = space*
__ = space+
__blank = $([ \n\t\r]*)

symbol = $([a-zA-Z_] [a-zA-Z0-9_]*)
dottableSymbol = $(symbol ('.' symbol)*)
hyphenizableSymbol = $([a-zA-Z_-] [a-zA-Z0-9_-]*)

reserved
  = SharedKeywords
  / JSKeywords

SharedKeywords
  = ("true" / "false" / "null" / "this" / "new" / "delete" / "typeof" /
  "instanceof" / "in" / "return" / "throw" / "break" / "continue" / "debugger" /
  "if" / "else" / "switch" / "for" / "while" / "do" / "try" / "catch" /
  "finally" / "class" / "extends" / "super") !symbol

JSKeywords
  = ("case" / "default" / "function" / "var" / "void" / "with" / "const" /
  "let" / "enum" / "export" / "import" / "native" / "implements" / "interface" /
  "package" / "private" / "protected" / "public" / "static" / "yield") !symbol
